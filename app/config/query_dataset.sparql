PREFIX a: <http://www.eea.europa.eu/portal_types/Data#>
PREFIX dt: <http://www.eea.europa.eu/portal_types/DataTable#>
PREFIX org: <http://www.eea.europa.eu/portal_types/Organisation#>
PREFIX daviz: <http://www.eea.europa.eu/portal_types/DavizVisualization#>
PREFIX gis: <http://www.eea.europa.eu/portal_types/GIS%%20Application#>
PREFIX eeafigure: <http://www.eea.europa.eu/portal_types/EEAFigure#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX ecodp: <http://open-data.europa.eu/ontologies/ec-odp#>
PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX datafilelink: <http://www.eea.europa.eu/portal_types/DataFileLink#>
PREFIX datafile: <http://www.eea.europa.eu/portal_types/DataFile#>
PREFIX sparql: <http://www.eea.europa.eu/portal_types/Sparql#>
PREFIX file: <http://www.eea.europa.eu/portal_types/File#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX cr: <http://cr.eionet.europa.eu/ontologies/contreg.rdf#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema: <http://schema.org/>
CONSTRUCT {
 ?dataset a dcat:Dataset;
  schema:productID ?product_id ;
  dct:title ?title;
  #workaround so we can have a default value if object is missing an attribute
  #this is needed because we are running on an older virtuoso, and has no support for BIND
  dct:description ?description_real;
  dct:description ?description_default;
  dct:issued ?effective;
  dct:modified ?modified;
  ecodp:keyword ?theme;
  dct:spatial ?pubspatial;
  dct:subject ?subject;
  dcat:theme ?dcat_theme;
  dct:isReplacedBy ?isreplaced;
  dct:replaces ?replaces.
 ?replaces a dcat:Dataset;
  dct:issued ?replaces_issued;
  dct:description ?replaces_description .
 ?dataset dcat:distribution ?datafile .
 ?datafile dcat:accessURL ?downloadUrl.
 ?datafile a <http://www.w3.org/TR/vocab-dcat#Download>;
  ecodp:distributionFormat ?format;
  dct:title ?dftitle;
  dct:modified ?dfmodified.
 ?dataset dcat:distribution ?related_item .
 ?related_item a ?related_item_type;
  ecodp:distributionFormat "text/html";
  dct:title ?related_item_title;
  dcat:accessURL ?related_item_url .
}
WHERE
{
 {
  ?dataset a a:Data ;
   a:id ?id;
   schema:productID ?product_id ;
   dct:title ?title.
  OPTIONAL { ?dataset dct:issued ?effective }
  OPTIONAL { ?dataset dct:modified ?modified }
  OPTIONAL { ?dataset dct:isReplacedBy ?isreplaced }
  OPTIONAL {
    ?dataset dct:replaces ?replaces .
    ?replaces dct:issued ?replaces_issued ;
      dct:description ?replaces_description .
  }
  #use the real description if available
  OPTIONAL { ?dataset dct:description ?description_real }
  #set a default description if object has no description
  OPTIONAL {
   {
    SELECT ("No description available" as ?description_default)
    WHERE
    {
     ?dataset a a:Data
     OPTIONAL { ?dataset dct:description ?description }
     FILTER (!bound(?description))
     FILTER (?dataset = <%(dataset)s> )
    }
   }
  }
 }
 UNION
 {
  ?dataset dct:hasPart ?datatable.
  ?datatable dct:hasPart ?datafile.
  {
   {
    SELECT DISTINCT ?datafile STRDT(bif:concat(?datafile,'/at_download/file'), xsd:anyURI) AS ?downloadUrl ?format
    WHERE
    {
     ?datafile a datafile:DataFile;
      dct:format ?format
     filter(str(?format) = "application/zip")
    }
   }
  }
  UNION
  {
   {
    SELECT DISTINCT ?datafile STRDT(bif:concat(?datafile,'/at_download/file'), xsd:anyURI) AS ?downloadUrl ?format
    WHERE
    {
     {
      SELECT DISTINCT ?datafile count(?format) as ?formatcnt
      WHERE
      {
       ?datafile a datafile:DataFile;
        dct:format ?format
       FILTER (str(?format) != 'application/zip')
      }
     }
     . FILTER (?formatcnt = 1)
     ?datafile dct:format ?format
    }
   }
  }
  UNION
  {
   {
    SELECT DISTINCT ?datafile STRDT(?remoteUrl, xsd:anyURI) AS ?downloadUrl 'application/octet-stream' AS ?format
    WHERE
    {
     ?datafile a datafilelink:DataFileLink;
      datafilelink:remoteUrl ?remoteUrl
    }
   }
  }
  UNION
  {
   {
    SELECT DISTINCT ?datafile STRDT(bif:concat(?datafile,'/download.csv'), xsd:anyURI) AS ?downloadUrl 'text/csv' as ?format
    WHERE
    {
     ?datafile a sparql:Sparql
    }
   }
  }
  UNION
  {
   {
    SELECT DISTINCT ?datafile STRDT(?datafile, xsd:anyURI) AS ?downloadUrl "file" as ?format
    WHERE {
     ?datafile a file:File
    }
   }
  }
  ?datafile dct:title ?dftitle .
  ?datafile dct:modified ?dfmodified
 }
 UNION
 {
  ?dataset a:relatedItems ?related_item .
  {
   SELECT DISTINCT ?related_item ?related_item_title (str(?related_item) as ?related_item_url)
   WHERE
   {
    ?related_item a ?related_item_type;
        dct:title ?related_item_title ;
        dct:expires ?related_item_expires .
    FILTER(?related_item_type IN (daviz:DavizVisualisation, eeafigure:EEAFigureFile, gis:GISApplication))
    FILTER(str(?related_item_expires) = "None")
   }
  }
 }
 UNION
 {
  ?dataset dct:subject ?subject
 }
 UNION
 {
  ?dataset dct:subject ?theme FILTER (isLiteral(?theme) && !REGEX(?theme,'[()/]'))
 }
 UNION
 {
  ?dataset dct:spatial ?spatial .
  ?spatial owl:sameAs ?pubspatial
  FILTER(REGEX(?pubspatial, '^http://publications.europa.eu/resource/authority/country/'))
 }
 UNION
 {
  ?dataset cr:tag ?tag.
  ?dcat_theme a skos:Concept.
  ?dcat_theme rdfs:label ?tag.
 }
 FILTER (?dataset = <%(dataset)s> )
}
